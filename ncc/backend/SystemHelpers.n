using Nemerle.IO;
using Nemerle.Imperative;
using Nemerle.Collections;
using Nemerle.Compiler.Typedtree;
using System.Linq;

using DN = dnlib.DotNet;
using SR  = System.Reflection;
using SRC = System.Runtime.CompilerServices;

namespace Nemerle.Compiler
{
  public module SystemHelpers
  {
    internal ToGenericResolver(this tenv : Map [string, StaticTypeVar]) : DN.GenericParam -> FixedType
    {
      fun (param)
      {
        if (tenv?.Find(param.FullName) is Some(tv))
          FixedType.StaticTypeVarRef(tv)
        else
          Util.ice($"Type parameter $(param) from $(param.DeclaringType)")
      }
    }

    internal GetMethod(this type : DN.TypeDef, name : string, params argumentTypes : array[DN.TypeDef]) : DN.MethodDef
    {
      foreach (method when method.Name == name && method.Parameters.Count == argumentTypes.Length in type.Methods)
      {
        def comparer = DN.TypeEqualityComparer.Instance;
        def parameters = method.Parameters;
        mutable isMatched = true;
        for (mutable index; isMatched && index < parameters.Count; ++index)
          isMatched &= comparer.Equals(parameters[index].Type, argumentTypes[index]);
        when (isMatched)
          return method;
      }

      Util.ice("Cannot find method $name(..$argumentTypes) in $type");
    }

    internal IsDefined(this attributeOwner : DN.IHasCustomAttribute, attribute : DN.ITypeDefOrRef) : bool
    {
      foreach (a in attributeOwner.CustomAttributes)
        when (DN.TypeEqualityComparer.Instance.Equals(attribute, a.AttributeType))
          return true;
      false
    }
    
    internal GetObsoletionDetails(this attributeOwner : DN.IHasCustomAttribute) : string * bool
    {
      mutable message : string;
      mutable isError;
      foreach (a in attributeOwner.CustomAttributes)
      {
        when (a.TypeFullName == "System.ObsoleteAttribute" && a.ConstructorArguments.Count <= 2)
        {
          when (a.ConstructorArguments.Count > 0)
            message = a.ConstructorArguments[0].Value :> DN.UTF8String;
          when (a.ConstructorArguments.Count > 1)
            isError = a.ConstructorArguments[1].Value :> bool;
        }
      }
      (message, isError)
    }

    internal GetConditions(this attributeOwner : DN.IHasCustomAttribute) : list[string]
    {
      mutable conditions = [];
      foreach (a in attributeOwner.CustomAttributes)
        when (a.TypeFullName == "System.Diagnostics.ConditionalAttribute" && a.ConstructorArguments.Count == 1)
          conditions ::= (a.ConstructorArguments[0].Value :> DN.UTF8String) : string;
      conditions
    }

    internal GetSystemType(typeInfo : TypeInfo) : DN.TypeDef
    {
      match (typeInfo)
      {
        | x is ExternalTypeInfo => x.SystemType
        | x is ITypeBuilder     => x.SystemType
        | _ => assert(false)
      }
    }

    internal GetSystemMemberInfo(member : IMember) : DN.IMemberDef
    {
      (member :> IBackendMember).GetHandle()
    }
    
    public GetNonVoidSystemType(type : TypeVar, systemObjectType : DN.TypeDef) : DN.IType
    {
      match (type)
      {
        | FixedType.Void => systemObjectType
        | _ => GetSystemType(type)
      }
    }

    public GetSystemType(type : TypeVar) : DN.IType
    {
      GetSystemType(type.Fix())
    }
    
    public GetSystemType(type : FixedType) : DN.IType
    {
      GetSystemType(type, ManagerClass.Instance.SystemTypeCache)
    }
    
    public GetSystemType(type : FixedType, systemTypeCache : SystemTypeClass) : DN.IType
    {
      match (type)
      {
        | Void                  => systemTypeCache.Void
        | Tuple                 => GetSystemType(TupleType.Make(type), systemTypeCache)
        | Fun                   => GetSystemType(FunctionType.Make(type), systemTypeCache)
        | StaticTypeVarRef(tv)  =>
          def genericParam = tv.GetSystemType();
          if (genericParam.DeclaringMethod != null)
            DN.GenericMVar(genericParam.Number, genericParam.DeclaringMethod)
          else
            DN.GenericVar(genericParam.Number, genericParam.DeclaringType)

        | Class(ti, [])         => GetSystemType(ti)
        | Class(ti, args)       =>
          def     tconstructor = GetSystemType(ti);
          def     typedargs    = array(args.Length);
          mutable idx          = 0;
          mutable formals      = ti.TypePparms;
          def     errorCount   = Message.ErrorCount;

          foreach (arg in args)
          {
            match (formals)
            {
              | f :: fs =>
                formals = fs;
                f.CheckConstraints(arg, type);

              | [] => Util.ice()
            }

            typedargs[idx] = ToTypeSig(GetSystemType(arg));
            ++idx;
          }

          if (errorCount == Message.ErrorCount)
            DN.GenericInstSig(if (tconstructor.IsValueType) DN.ValueTypeSig(tconstructor) else DN.ClassSig(tconstructor), typedargs);
          else
            systemTypeCache.Object

        | Ref(t) | Out(t)      => DN.Extensions.ToByRefSig(ToTypeSig(GetSystemType(t)))
        | Array(et, rank)      =>
          def et_SystemType = GetSystemType(et);
          when (et_SystemType.Equals(systemTypeCache.Void))
            Message.FatalError("array element type cannot be void");
          if (rank == 1)
            DN.SZArraySig(ToTypeSig(et_SystemType))
          else
            DN.ArraySig(ToTypeSig(et_SystemType), rank)

        | Intersection(types)   => Message.FatalError($"The intersection of types has detect. Please, specify type manually. Intersected types: ..$(types)");
      }
    }

    private ToTypeSig(type : DN.IType) : DN.TypeSig
    {
      | typeSig is DN.TypeSig       => typeSig
      | argType is DN.ITypeDefOrRef => DN.Extensions.ToTypeSig(argType, true)
      | _ => assert(false)
    }
  }
} // end ns
