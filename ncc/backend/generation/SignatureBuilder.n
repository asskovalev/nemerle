using Nemerle.Collections;
using Nemerle.Compiler.Backend;
using Nemerle.Compiler.Typedtree;
using Nemerle.Utility;

using DN = dnlib.DotNet;

namespace Nemerle.Compiler
{
  [ManagerAccess]
  class SignatureBuilder
  {
    private _module_builder   : DN.ModuleDef;

    public this(manager : ManagerClass)
    {
      Manager         = manager;
      _module_builder = Manager.Hierarchy.GetModuleBuilder();
    }

    public GetMethodInfo(from_type : TypeVar, meth : IMethod, typars : list [TypeVar]) : DN.IMethod
    {
      def methodRef        = (meth :> IBackendMethod).MethodRef;
      def system_from_type = ToSuperTypeSig(from_type, meth.DeclaringType);

      mutable result : DN.IMethod =
        if (system_from_type is DN.GenericInstSig)
          DN.MemberRefUser(methodRef.Module, methodRef.Name, methodRef.MethodSig, DN.TypeSpecUser(system_from_type))
        else
          methodRef;

      unless (typars.IsEmpty)
      {
        def m_typarms = meth.GetHeader().TypeParameters;
        def errorCount = Message.ErrorCount;
        NList.Iter2 (m_typarms, typars, _.CheckConstraints (_, meth));
        when (errorCount == Message.ErrorCount)
        {
          def pars  = typars.MapToArray(ToNonVoidTypeSig);
          result = DN.MethodSpecUser(result :> DN.IMethodDefOrRef, DN.GenericInstMethodSig(pars));
        }
      }
      result
    }

    public GetFieldInfo(from_type : TypeVar, field : IField) : DN.IField
    {
      def fieldRef = (field :> IBackendField).FieldRef;
      def obj_type = ToSuperTypeSig(from_type, field.DeclaringType);
      if (obj_type is DN.GenericInstSig)
        DN.MemberRefUser(fieldRef.Module, fieldRef.Name, fieldRef.FieldSig, DN.TypeSpecUser(obj_type))
      else
        fieldRef
    }
    
    private static ToSuperTypeSig(type : TypeVar, superType : TypeInfo) : DN.TypeSig
    {
      def type = type.Fix();
      (if (type is FixedType.Tuple) type else type.GetInstantiatedSuperType(superType)).ToTypeSig()
    }

    private ToNonVoidTypeSig(type : TypeVar) : DN.TypeSig
    {
      def corlibTypes = _module_builder.CorLibTypes;
      match (type.Fix())
      {
        | FixedType.Void => corlibTypes.Object
        | t => SystemHelpers.ToTypeSig(t, corlibTypes)
      }
    }
  } // end of the class
} // end of the namespace
