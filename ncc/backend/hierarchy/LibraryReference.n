using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Compiler.Typedtree;
using Nemerle.IO;
using Nemerle.Late;
using Nemerle.Utility;
using System.IO;
using System.Runtime.CompilerServices;
using System.Collections.ObjectModel;

using DN = dnlib.DotNet;
using PT = Nemerle.Compiler.Parsetree;
using PExpr = Nemerle.Compiler.Parsetree.PExpr;
using SR = System.Reflection;
using SCG = System.Collections.Generic;

namespace Nemerle.Compiler
{
  /// This class stores information extracted from a referenced external library.
  [ManagerAccess]
  public partial class LibraryReference
  {
    /// The assembly associated with this object
    public Assembly : SR.Assembly { get; }

    public ModuleDef : DN.ModuleDefMD { get; }

    public Location : Location { get { _location } }

    /// The location of this library
    private mutable _location : Location;

    public IsFriend : bool { get; }

    public IsUserReference : bool { get; internal set; }

    /// If set to true, the current assembly declares itself
    /// to be generated by the Nemerle compiler.
    _is_generated_by_nemerle : bool;

    public LibRefManager : LibraryReferenceManager;

    private LookupAttributeType(attr_name : string, attr : ref DN.TypeDef, attr_loc : ref string) : void
    {
      def ext = ModuleDef.Find(attr_name, true);

      // HACK: VladD2: In .Net 4.5 ExtensionAttribute define twice.
      when (ext != null && attr != null && attr.FullName != "System.Runtime.CompilerServices.ExtensionAttribute")
      {
        Message.Warning ($"$attr_name attribute from $ModuleDef is ignored");
        Message.HintOnce ($"$attr_name is first defined in $attr_loc");
      }

      def new_attr_loc = ModuleDef.ToString();

      when (ext != null && (attr == null || attr_loc == new_attr_loc))
      {
        attr = ext;
        attr_loc = new_attr_loc;
      }
    }

    /// Load an assembly. Extracts and processes the custom attributes array.
    internal this (mgr : LibraryReferenceManager, assembly : SR.Assembly, isUserReference : bool)
    {
      LibRefManager = mgr;
      Manager = mgr.Manager;
      ModuleDef = DN.ModuleDefMD.Load(assembly.Location, mgr.Context);
      Assembly = assembly;
      IsUserReference = isUserReference;
      _location = Location (Location.GetFileIndex (mgr.getAssemblyLocation(assembly)), 0, 0);
      _location.MarkAsGenerated();

      def currentAssemblyName = Path.GetFileNameWithoutExtension(this.Manager.Options.OutputFileName);
      def snKey() : SR.StrongNameKeyPair
      {
        using (keyFile = File.Open(this.Manager.Options.StrongAssemblyKeyName, FileMode.Open, FileAccess.Read))
          SR.StrongNameKeyPair(keyFile)
      }
      def toHexString(bytes : array[byte]) : string
      {
        def hex = System.BitConverter.ToString(bytes);
        hex.Replace("-","");
      }
      def generatePublicKeyToken(publicKey : array[byte]) : array[byte]
      {
        def sha = System.Security.Cryptography.SHA1Managed();
        def hash = sha.ComputeHash(publicKey);
        def len = 8;
        mutable result : array[byte] = array(len);
        System.Array.Copy(hash, hash.Length - len , result, 0, len);
        System.Array.Reverse(result, 0, len);
        result;
      }

      // Scans the assembly custom attributes looking for something interesting...
      foreach (a in ModuleDef.CustomAttributes)
      {
        match (a.TypeFullName)
        {
          | "System.Reflection.AssemblyConfigurationAttribute" when !_is_generated_by_nemerle && a.HasConstructorArguments =>
            when (a.ConstructorArguments[0].Value is DN.UTF8String as configuration)
              _is_generated_by_nemerle = "ContainsNemerleTypes".Equals(configuration : string);

          | "System.Runtime.CompilerServices.InternalsVisibleToAttribute" when !IsFriend && a.HasConstructorArguments =>
            when (a.ConstructorArguments[0].Value is DN.UTF8String as assemblyName)
            {
              def parts = (assemblyName : string).Replace(" ", "").SplitToList(array[',']);
              match (parts)
              {
                | [asmName] when currentAssemblyName == asmName => IsFriend = true
                | [asmName, pKey] when string.IsNullOrEmpty(this.Manager.Options.StrongAssemblyKeyName) && currentAssemblyName == asmName =>
                  match(pKey.ToLower().SplitToList(array[',']))
                  {
                    | ["publickey", key] when toHexString(snKey().PublicKey).ToLower() == key.ToLower()
                    | ["publickeytoken", key] when toHexString(generatePublicKeyToken(snKey().PublicKey)).ToLower() == key.ToLower() => IsFriend = true
                    | _ => ()
                  }

                | _ => ()
              }
            }

          | _ => ()
        }
      }

      LookupAttributeType("Nemerle.Internal.ExtensionAttribute", ref SystemTypeCache.ExtensionAttribute, ref SystemTypeCache.ExtensionAttributeAssembly);
      LookupAttributeType("System.Runtime.CompilerServices.ExtensionAttribute", ref SystemTypeCache.SQ_ExtensionAttribute, ref SystemTypeCache.SQ_ExtensionAttributeAssembly);
    }

    internal LoadContents () : void
    {
      LibRefManager.LoadTypesFrom(this);
      LibRefManager.LoadMacrosFrom(Assembly);
    }

    public override ToString () : string
    {
      if (_location == Location.Default) Assembly.ToString()
      else if (Assembly != null) _location.ToString()
      else base.ToString()
    }

    /// Turns a Framework type into something edible by Nemerle's type system
    internal TypeOfType(_tenv : Map [string, StaticTypeVar], framework_type : DN.ITypeDefOrRef, rawInternals : bool = false) : FixedType
    {
      TypeOfType(_tenv.ToGenericResolver(), framework_type, rawInternals)
    }

    /// Turns a Framework type into something edible by Nemerle's type system
    internal TypeOfType(genericResolver : DN.GenericParam -> FixedType, framework_type : DN.ITypeDefOrRef, rawInternals : bool = false) : FixedType
    {
      if (DN.TypeEqualityComparer.Instance.Equals(framework_type, SystemTypeCache.Void))
        InternalType.Void
      else if (framework_type is DN.TypeSpec(TypeSig = sig))
        TypeOfType(genericResolver, sig, rawInternals)
      else
        CreateTypeSpecialization(framework_type, [], rawInternals)
    }

    /// Turns a Framework type into something edible by Nemerle's type system
    internal TypeOfType(_tenv : Map [string, StaticTypeVar], framework_type : DN.TypeSig, rawInternals : bool = false) : FixedType
    {
      TypeOfType(_tenv.ToGenericResolver(), framework_type, rawInternals)
    }

    internal TypeOfType(genericResolver : DN.GenericParam -> FixedType, framework_type : DN.TypeSig, rawInternals : bool = false) : FixedType
    {
      match (framework_type)
      {
        | t is DN.TypeDefOrRefSig => TypeOfType(genericResolver, t.TypeDefOrRef, rawInternals)
        | t is DN.ArraySigBase    => FixedType.Array(TypeOfType(genericResolver, framework_type.Next, rawInternals), t.Rank :> int)
        | _ is DN.ByRefSig        => TypeOfType(genericResolver, framework_type.Next, rawInternals)
        | t is DN.GenericSig      => genericResolver(t.GenericParam)
        | t is DN.GenericInstSig =>
          def typarms = t.GenericArguments.Map(TypeOfType(genericResolver, _, rawInternals));
          CreateTypeSpecialization(t.GenericType.TypeDefOrRef, typarms, rawInternals)

        | _ is DN.FnPtrSig | _ is DN.PtrSig => InternalType.IntPtr  // TODO: поддержать указатели
        | _ =>
          assert2(false, $"Unknown type signature $framework_type");
          InternalType.Object
      }
    }

    private CreateTypeSpecialization(framework_type : DN.ITypeDefOrRef, parms : list[TypeVar], rawInternals : bool) : FixedType
    {
      def tc = TypeInfoOfType(framework_type);

      if (framework_type.Namespace == "Nemerle.Builtins" && !rawInternals)
      {
        def make_tupl(l)
        {
          | [x] => x
          | []  => InternalType.Void
          | _   => FixedType.Tuple(l)
        }

        if (tc.Name == "Tuple")
          FixedType.Tuple(parms)
        else if (tc.Name == "FunctionVoid")
          FixedType.Fun(make_tupl(parms), parms.Length, InternalType.Void)
        else
        {
          def (front, last) = parms.DivideLast();
          FixedType.Fun(make_tupl(front), front.Length, last)
        }
      }
      else FixedType.Class(tc, parms);
    }

    mutable doEnsureCached : bool = false;

    // TODO: VladD2: Move this method into LibraryReferenceManager and make it independetnt from LibraryReference!
    internal TypeInfoOfType (framework_type : DN.ITypeDefOrRef) : TypeInfo
    {
      def typeDef =
        match (framework_type)
        {
          | DN.TypeSpec(TypeSig = DN.TypeDefOrRefSig() as sig)
          | DN.TypeSpec(TypeSig = DN.GenericInstSig(GenericType = sig)) => sig.TypeDef ?? sig.TypeRef.ResolveThrow()
          | DN.TypeDef as typeDef => typeDef
          | DN.TypeRef as typeRef => typeRef.ResolveThrow()
          | _ => Util.ice($"Unknown type reference $framework_type");
        };

      def ns_node_type = LibRefManager.framework_nesting(framework_type);

      when (doEnsureCached)
        ns_node_type.EnsureCached();

      match (ns_node_type.LookupType ([], typeDef.GenericParameters.Count))
      {
        | Some (tc) => tc
          // protected external types are not fetched automatically from external assembly
          // so sometimes we must load them by hand
        | None => LibraryReferenceManager.GetInternalType(this, typeDef, ns_node_type)
      }
    }

    /// Construct TypeInfo object from given type in current assembly.
    internal ConstructTypeInfo (reflected_type : DN.TypeDef, ns_node : NamespaceTree.Node) : TypeInfo
    {
      if (_is_generated_by_nemerle && reflected_type.IsDefined (SystemTypeCache.NemerleAttribute))
        ExternalNemerleTypeInfo (this, reflected_type, ns_node)
      else if (reflected_type.IsPrimitive)
        ExternalPrimitiveTypeInfo (this, reflected_type, ns_node)
      else
        ExternalTypeInfo (this, reflected_type, ns_node)
    }

    /// Looks for type named [name] that is internal to current assembly. Used
    /// by type attribute decoder.
    internal LookupInternalType (name : string) : option [TypeInfo]
    {
    //  Message.Debug ($"looking `$(name)'");
      def st = ModuleDef.Find(name, true);

      if (st == null)
        None ()
      else
      {
        def ns = LibRefManager.framework_nesting (st);
        Some (LibraryReferenceManager.GetInternalType (this, st, ns))
      }
    }

    internal MethodOfMethodInfo(tenv : Map[string, StaticTypeVar], meth : DN.MethodDef) : ExternalMethodInfo
    {
      if (meth == null) null
      else              ExternalMethodInfo(tenv, this, meth)
    }

    public GetCustomAttributes() : list[PExpr * list[PExpr]]
    {
      ConvertCustomAttributesData(ModuleDef, this)
    }
    
    internal static ConvertCustomAttributesData(attributeOwner : DN.IHasCustomAttribute, library : LibraryReference) : list[PExpr * list[PExpr]]
    {
      def manager = library.Manager;
      def result = SCG.List();

      foreach (attr in attributeOwner.CustomAttributes)
      {
        def name = PExpr.FromQualifiedIdentifier(manager, attr.TypeFullName);
        def makeArg(a : DN.CAArgument) : PExpr
        {
          def lift(value : object) : PExpr
          {
            | value is bool           => <[ $(value :  bool   ) ]>
            | value is DN.UTF8String  => <[ $(value :  string ) ]> 
            | value is double         => <[ $(value :  double ) ]> 
            | value is float          => <[ $(value :  float  ) ]> 
            | value is decimal        => <[ $(value :  decimal) ]> 
            | value is int            => <[ $(value :  int    ) ]> 
            | value is uint           => <[ $(value :  uint   ) ]> 
            | value is long           => <[ $(value :  long   ) ]> 
            | value is ulong          => <[ $(value :  ulong  ) ]> 
            | value is short          => <[ $(value :  short  ) ]> 
            | value is ushort         => <[ $(value :  ushort ) ]> 
            | value is byte           => <[ $(value :  byte   ) ]> 
            | value is sbyte          => <[ $(value :  sbyte  ) ]> 
            | value is char           => <[ $(value :  char   ) ]> 
            | value is System.Guid    => <[ System.Guid($(value.ToString() : string)) ]>
            | value is DN.TypeSig     => <[ typeof($(library.TypeOfType(null, value) : typed)) ]>
            | col is SCG.IList[DN.CAArgument] => <[ array[..$(col.MapToList(e => lift(e.Value)))] ]>
            | value => Message.FatalError($"Custom atribute value ($value) of type $(value.GetType()) not suported yet.")
          }

          lift(a.Value)
        }
        def args = attr.ConstructorArguments.MapToList(makeArg);
        result.Add((name, args));
      }
      
      result.NToList()
    }
  }
} /* namespace */
