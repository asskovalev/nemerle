using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Compiler.Typedtree;
using Nemerle.IO;
using Nemerle.Late;
using Nemerle.Utility;
using System.IO;

using DN = dnlib.DotNet;
using PT = Nemerle.Compiler.Parsetree;  
using SR = System.Reflection;
using SRE = System.Reflection.Emit;
using SCG = System.Collections.Generic;

namespace Nemerle.Compiler 
{
  public class ExternalEventInfo : ExternalMemberInfo, IEvent, IBackendMember, IBackendEvent
  {
   [Accessor]
    protected handle   : DN.EventDef;
    protected adder    : ExternalMethodInfo;
    protected remover  : ExternalMethodInfo;
    protected mem_type : FixedType;

    public this (lib : LibraryReference, h : DN.EventDef)
    {
      handle = h;
      base (lib);

      def tenv = (DeclaringType :> ExternalTypeInfo).tenv;
      adder = library.MethodOfMethodInfo(tenv, handle.AddMethod);
      remover = library.MethodOfMethodInfo (tenv, handle.RemoveMethod);

      assert(adder != null);
      assert(remover != null);

      match (adder.GetMemType ()) 
      {
        | FixedType.Fun (t, _) => mem_type = t.FixedValue;
      }
      
      attributes = adder.Attributes;
    }

    public DeclaringType : TypeInfo
    {
      //[Nemerle.Memoize (InvalidValue = null)]
      get { library.TypeInfoOfType (handle.DeclaringType) }
    }
    
    public Name       : string      { get { handle.Name } }
    public MemberKind : MemberKinds { get { MemberKinds.Event } }

    public GetAdder()     : IMethod      { adder }
    public GetRemover()   : IMethod      { remover }
    public GetMemType()   : FixedType        { mem_type }

    public CanAccess (_source : TypeInfo) : bool
    {
      // FIXME: this is broken... we need to check method attributes
      true
    }

    GetHandle() : DN.IMemberDef
      implements IBackendMember.GetHandle
    { handle }
    
    GetEventInfo() : DN.EventDef
      implements IBackendEvent.GetEventInfo
    { handle }
  
    public override ToString () : string { MemberBuilder.DescribeMember (this) }
  } // class
} // namespace
