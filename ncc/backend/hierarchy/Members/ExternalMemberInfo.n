using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Compiler.Typedtree;
using Nemerle.IO;
using Nemerle.Late;
using Nemerle.Utility;
using System;
using System.IO;

using PT = Nemerle.Compiler.Parsetree;
using SR = System.Reflection;
using SRE = System.Reflection.Emit;
using SCG = System.Collections.Generic;

namespace Nemerle.Compiler
{
  public abstract class ExternalMemberInfo : MemberInfo, System.IEquatable[ExternalMemberInfo], IBackendMember
  {
    GetHandle() : SR.MemberInfo
      implements IBackendMember.GetHandle
    { throw NotImplementedException() }
    
    public virtual NameLocation : Location { get { this.Location } }

    [Accessor]
    protected library : LibraryReference;
    protected id      : int;

    protected InternalType    : InternalTypeClass { get { library.Manager.InternalType } }
    protected SystemTypeCache : SystemTypeClass   { get { library.Manager.SystemTypeCache } }

    public this(lib : LibraryReference)
    {
      library = lib;
      id = lib.Manager.GetNewId();

      when (InternalType.Obsolete_tc != null)
        is_obsolete = HasAttribute (InternalType.Obsolete_tc);
    }

    internal override GetObsoletionDetails() : string * bool
    {
      def attr = System.Attribute.GetCustomAttribute(
        (this : IBackendMember).GetHandle(),
        SystemHelpers.GetSystemType(InternalType.Obsolete_tc),
        false);
      assert(attr != null);
      (late(attr.Message) :> string, late(attr.IsError) :> bool)
    }

    public HasAttribute (attribute : TypeInfo) : bool
    {
      def t = SystemHelpers.GetSystemType(attribute);
      (this : IBackendMember).GetHandle().IsDefined (t, false)
    }

    public Location : Location { get { library.Location } }

    public override GlobalEnv : GlobalEnv { get { library.Manager.CoreEnv } }

    [Memoize]
    public override GetModifiers () : AttributesAndModifiers
    {
      def attrsData = SR.CustomAttributeData.GetCustomAttributes((this : IBackendMember).GetHandle());
      def attrs     = LibraryReference.ConvertCustomAttributesData(attrsData, library);
      def attrAsts  = attrs.Map((name, args) => <[ $name(..$args)]>);
      AttributesAndModifiers(Attributes, attrAsts)
    }

    public override IsCustomAttributeDefined(attributeFullName : string) : bool
    {
      GetModifiers().IsCustomAttributeDefined(attributeFullName, library.Manager.CoreEnv)
    }

    [Nemerle.OverrideObjectEquals]
    public Equals (o : ExternalMemberInfo) : bool
      implements System.IEquatable[ExternalMemberInfo].Equals
    {
      assert(o != null);
      def x = (this : IBackendMember).GetHandle();
      def y = (o : IBackendMember).GetHandle();
      try { id == o.id || y.Equals(x) }
      catch
      { _ =>
        try { x.Equals(y) }
        catch
        { _ =>
          false
        }
      }
    }

    public override GetHashCode () : int { id }
  }
} /* namespace */
