using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler.Backend;
using Nemerle.Imperative;
using System;
using System.Reflection;

using DN = dnlib.DotNet;
using NC = Nemerle.Compiler;
using PT = Nemerle.Compiler.Parsetree;

namespace Nemerle.Compiler
{
  public partial class StaticTypeVar
  {
    mutable _systemType : DN.GenericParam;
    
    public this(man : ManagerClass, name : string, gtype : DN.GenericParam)
    {
      this(man, name);
      _systemType = gtype;
    }
    
    public GetSystemType() : DN.GenericParam
    {
      assert(_systemType != null, ToString());
      _systemType
    }
  
    internal SetGenericBuilder(x : DN.GenericParamUser) : void
    {
      Util.cassert(_systemType == null);
      Util.cassert(x != null);
      x.SpecialConstraint = special;
      _systemType = x;
    }

    internal UpdateConstraints(importer : ref DN.Importer) : void
    {
      foreach (c in constraints)
      {
        def constraint =
          match (c.GetSystemType(ref importer))
          {
            | sig is DN.TypeDefOrRefSig => sig.TypeDefOrRef
            | sig                       => DN.TypeSpecUser(sig)
          };

        _systemType.GenericParamConstraints.Add(DN.GenericParamConstraintUser(constraint));
      }
    }
  }
}