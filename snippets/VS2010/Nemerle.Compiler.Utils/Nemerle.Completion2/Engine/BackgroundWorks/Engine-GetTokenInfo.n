using System;
using System.IO;
using System.Diagnostics;
using SCG = System.Collections.Generic;
using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Utils.Async;
using Nemerle.Imperative;
using Nemerle.Surround;
using Nemerle.Utility;

using Nemerle.Compiler.Utils;
using Typed = Nemerle.Compiler.Typedtree;
using SR = System.Reflection;
using Nemerle.Completion2;

namespace Nemerle.Completion2
{
  internal partial class Engine
  {
    public BeginGetQuickTipInfo(source : IIdeSource, at : Location) : QuickTipInfoAsyncRequest
    {
      def request = QuickTipInfoAsyncRequest(AsyncRequestType.GetQuickTipInfo, this, source, GetQuickTipInfo, at);
      AsyncWorker.AddWork(request);
      request
    }

    private GetQuickTipInfo(request : AsyncRequest) : void
    {
      AsyncWorker.CheckCurrentThreadIsTheAsyncWorker();

      surroundwith (currentAsyncRequest)
      try
      {
        if (IsBuildTypesTreeInProgress)
          AsyncWorker.AddWork(request);
        else
        {
          def project = CheckProjectAvailable(request);

          when (project != null)
          {
            def req = request :> QuickTipInfoAsyncRequest;
            req.QuickTipInfo = project.GetQuickTipInfo(req.At);
            request.MarkAsCompleted();
            _ = BeginHighlightUsages(request.Source, req.At);
          }
        }
      }
      catch
      { | e is CompilationAbortedException =>
          def msg = $"The GetQuickTipInfo operation aborted at: $(e.Message)";
          throw CompilationAbortedException(msg, e);
      }
    }
  } // end class Engine
} // end namespace
